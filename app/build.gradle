/*
 *     Dharmaseed Android app
 *     Copyright (C) 2016  Brett Bethke
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

apply plugin: 'com.android.application'

android {

    defaultConfig {
        applicationId "org.dharmaseed.android"
        minSdk 21
        compileSdk 34
        targetSdk 34
        versionCode 32
        versionName "1.7.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    /*
    For signing to work, create a file ~/.gradle/gradle.properties containing:

    DS_STORE_FILE=../../relative/path/to/your/store.jks
    DS_STORE_PASSWORD=*****
    DS_KEY_ALIAS=*****
    DS_KEY_PASSWORD=*****

    (adapted from https://stackoverflow.com/a/21020469)
    NOTES:
     - no quotes around any ***** values
     - the DS_STORE_FILE path is relative to the directory containing this file
     */
    signingConfigs {
        if (project.hasProperty('DS_STORE_FILE')) {
            defaultConfig {
                storeFile file(DS_STORE_FILE)
                storePassword DS_STORE_PASSWORD
                keyAlias DS_KEY_ALIAS
                keyPassword DS_KEY_PASSWORD

                // Optional, specify signing versions used
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty('DS_STORE_FILE')) {
                signingConfig signingConfigs.defaultConfig
            }
        }
        debug {
            if (project.hasProperty('DS_STORE_FILE')) {
                signingConfig signingConfigs.defaultConfig
            }
        }
    }
    namespace 'org.dharmaseed.android'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.2'

    // UI stuff
    implementation("androidx.swiperefreshlayout:swiperefreshlayout:1.1.0")

    // Exoplayer stuff
    implementation 'androidx.media3:media3-exoplayer:1.3.0'
    implementation 'androidx.media3:media3-ui:1.3.0'
    implementation "androidx.media3:media3-session:1.3.0"

    // Required for instrumented tests
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
}
